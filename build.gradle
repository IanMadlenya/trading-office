apply from: "$rootDir/libraries.gradle"

buildscript {
    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/vermeulen-mp/gradle-plugins' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE"
        classpath 'com.wiredforcode:gradle-heroku-plugin:1.0.0'
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.0.2'
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'heroku'
}

subprojects {
    repositories {
        mavenLocal()
        maven {
            url "http://repo1.maven.org/maven2"
            artifactUrls "https://repository.jboss.org/nexus/content/groups/public"
        }
        jcenter()
        maven { url "http://maven-repository.openspaces.org" }
        maven { url "http://repository.springsource.com/maven/bundles/external" }
    }

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.unbroken-dome.test-sets'

    sourceCompatibility = 1.8
    version = '1.0'

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check.dependsOn jacocoTestReport

    test {
        testLogging {
            exceptionFormat = 'full'
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }

    testSets {
        integrationTest
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test

    project.integrationTest {
        group = 'verification'
        testLogging {
            exceptionFormat = 'full'
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
        outputs.upToDateWhen { false }
    }

    tasks.withType(Test) {
        reports.html.destination = file("${reporting.baseDir}/${name}")
    }
}

defaultTasks 'clean', 'bootRepackage'

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
