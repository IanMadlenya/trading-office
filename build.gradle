buildscript {
    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/vermeulen-mp/gradle-plugins' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE"
    }
}

plugins {
    id "org.sonarqube" version "1.2"
    id 'org.unbroken-dome.test-sets' version '1.2.0'
}

apply plugin: 'idea'
apply plugin: 'checkstyle'
apply from: "$rootDir/libraries.gradle"

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
    }
}

subprojects {
    repositories {
        mavenLocal()
        maven {
            url "http://repo1.maven.org/maven2"
            artifactUrls "https://repository.jboss.org/nexus/content/groups/public"
        }
        jcenter()
        maven { url "http://maven-repository.openspaces.org" }
        maven { url "http://repository.springsource.com/maven/bundles/external" }
    }

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.unbroken-dome.test-sets'

    sourceCompatibility = 1.8
    version = '1.0'

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check.dependsOn jacocoTestReport

    test {
        testLogging {
            exceptionFormat = 'full'
        }
    }

    testSets {
        integrationTest
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test

    project.integrationTest {
        group = 'verification'
        testLogging {
            exceptionFormat = 'full'
        }
    }

    tasks.withType(Test) {
        reports.html.destination = file("${reporting.baseDir}/${name}")
    }

    task jacocoIntegrationTestReport(type: JacocoReport) {
        sourceSets sourceSets.main
        executionData integrationTest
    }
}

defaultTasks 'clean', 'bootRepackage'

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
