buildscript {
    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/vermeulen-mp/gradle-plugins' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE"
    }
}

plugins {
    id "org.sonarqube" version "1.2"
}

apply from: "$rootDir/gradle/versioning.gradle"
apply from: "$rootDir/libraries.gradle"

def sonarLogin = hasProperty('sonarLogin') ? sonarLogin : System.getenv('SONAR_LOGIN')
def sonarPassword = hasProperty('sonarPassword') ? sonarPassword : System.getenv('SONAR_PASSWORD')
def sonarHostUrl = hasProperty('sonarHostUrl') ? sonarHostUrl : System.getenv('SONAR_HOST_URL')

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.host.url", sonarHostUrl
        property "sonar.login", sonarLogin
        property "sonar.password", sonarPassword
    }
}

subprojects {
    repositories {
        mavenLocal()
        maven {
            url "http://repo1.maven.org/maven2"
            artifactUrls "https://repository.jboss.org/nexus/content/groups/public"
        }
        jcenter()
        maven { url "http://maven-repository.openspaces.org" }
        maven { url "http://repository.springsource.com/maven/bundles/external" }
    }

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'

    apply from: "$rootDir/gradle/jacoco.gradle"


    sourceCompatibility = 1.8

    test {
        testLogging {
            exceptionFormat = 'full'
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }
}

defaultTasks 'clean', 'bootRepackage'

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
