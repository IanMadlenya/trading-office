ext {
    libs = libsMap(
            jaxen: 'jaxen:jaxen:1.1.6',
            jdom: 'jdom:jdom:1.1',
            jackson: 'com.fasterxml.jackson.core:jackson-core:2.6.3',
            jackson_databind: 'com.fasterxml.jackson.core:jackson-databind:2.6.3',
            jackson_annotations: 'com.fasterxml.jackson.core:jackson-annotations:2.6.3',
            guava: 'com.google.guava:guava:19.0',
            activemq: 'org.apache.activemq:activemq-broker',
            activemq_kahadb: 'org.apache.activemq:activemq-kahadb-store',
            activemq_http: 'org.apache.activemq:activemq-http',
            spring: [
                boot: 'org.springframework.boot:spring-boot-starter',
                boot_actuator: 'org.springframework.boot:spring-boot-starter-actuator',
                boot_web: 'org.springframework.boot:spring-boot-starter-web',
                jms: 'org.springframework:spring-jms'
            ],
            test: [
                    junit: 'junit:junit:4.11',
                    assertj: 'org.assertj:assertj-core:1.7.1',
                    spock: 'org.spockframework:spock-core:1.0-groovy-2.4',
                    mockito: 'org.mockito:mockito-core:2.0.38-beta'
            ],
            groovy: 'org.codehaus.groovy:groovy-all:2.4.1',
            jasperreports: dependencies.create('net.sf.jasperreports:jasperreports:6.2.0') {
                exclude group: 'org.olap4j', module: 'olap4j'
            },
            itext: 'com.lowagie:itext:2.1.7'
    )
}

def libsMap(Map<String, ?> map) {
    def processMap
    processMap = { innerMap ->
        innerMap.each {
            if (it.value instanceof Map) {
                innerMap[it.key] = processMap(it.value)
            }
        }
        innerMap.asImmutable().withDefault {
            throw new InvalidUserDataException(
                    "No entry '$it' in dependency map (candidates: ${innerMap.keySet().sort().join(", ")})" )
        }
    }
    processMap(map)
}
